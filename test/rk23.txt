/*
	Micromagnetic standard problem 4 (a) according to
	http://www.ctcms.nist.gov/~rdm/mumag.org.html
*/

setSolver(3)
//maxErr = 1e-5
//headroom=1

// geometry
	setgridsize(128, 32, 1)
	setcellsize(500e-9/128, 125e-9/32, 3e-9)

// material
	Msat = 800e3
	Aex = 13e-12
	m  = uniform(1, .1, 0)

	tableAutoSave(1e-12)
// relax
	alpha = 3
	runwhile(maxtorque > 1e-4)
	run(1e-9)
	expectv("m", m.average(), vector(0.96696, 0.12528, 0), 1e-3)


// reversal
	eval0 := NEval()
	autosave(m, 10e-12)
	tableAutoSave(5e-12)
	alpha = 0.02
	B_ext = vector(-24.6E-3, 4.3E-3, 0)
	run(1e-9)
	expectv("m", m.average(), vector(-0.9845, 0.1268, 0.0432), 1e-3)

	print(NEval()-eval0, "evaluations")
