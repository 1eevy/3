/*
	Micromagnetic standard problem 4 (a) according to
	http://www.ctcms.nist.gov/~rdm/mumag.org.html
*/

// geometry
	setgridsize(128, 32, 1)
	setcellsize(500e-9/128, 125e-9/32, 3e-9)

// material
	Msat = 800e3
	Aex = 13e-12
	m  = uniform(1, .1, 0)

// relax
	alpha = 3
	run(10e-9)
	TOL := 1e-5
	//print(m.average())
	expectv("m", m.average(), vector(0.9669623970985413, 0.12529028952121735, 0), TOL)

// reversal
	setSolver(3)
	eval0 := NEval()
	autosave(m, 10e-12)
	tableAutoSave(5e-12)
	alpha = 0.02
	B_ext = vector(-24.6E-3, 4.3E-3, 0)
	run(1e-9)
	//print(m.average())
	expectv("m", m.average(), vector(-0.984616219997406, 0.12605585157871246, 0.04327124357223511), TOL)
	print(NEval()-eval0, "evaluations")
